plugins {
    id 'com.craigburke.karma' version '1.4.4'
    id 'org.springframework.boot' version '2.2.1.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

node {
    version = '11.12.0'
}

karma {
    basePath = '../' // <1>
    colors = true // <2>

    browsers = ['PhantomJS'] // <4>
    frameworks = ['jasmine'] // <5>

    profile('default') {
        libraryBases = ['src/main/resources/static/lib/']
        libraryFiles = ['jquery-3.2.1.js'] // <1>

        sourceBases = ['src/main/resources/static/js/']
        sourceFiles = ['*.js'] // <2>

        testBases = ['src/test/javascript/']
        testFiles = ['**/*test.js'] // <3>
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'e2e', 'src/test/resources', '--tags', 'not @developing']
        }
    }
}

task cucumber_focus() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'e2e', 'src/test/resources', '--tags', '@focus']
        }
    }
}

dependencies {
    def cucumberVer = "4.7.4"
    def jacksonVer = "2.10.0"

    compile(
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-thymeleaf",
            "org.apache.commons:commons-email:1.5",
            "commons-io:commons-io:2.6",
            "org.apache.commons:commons-lang3:3.9",
            "com.fasterxml.jackson.core:jackson-core:2.10.0",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVer",
            "com.fasterxml.jackson.core:jackson-annotations:$jacksonVer",
            "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jacksonVer"
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'com.icegreen:greenmail:1.5.11',
            'junit:junit:4.12',
            'org.mockito:mockito-core:3.2.0',
            'io.github.bonigarcia:webdrivermanager:3.7.1',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            "io.cucumber:cucumber-java:$cucumberVer",
            "io.cucumber:cucumber-junit:$cucumberVer",
            "io.cucumber:cucumber-spring:$cucumberVer"
    )
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}