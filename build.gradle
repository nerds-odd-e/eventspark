plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id "io.freefair.lombok" version "5.3.0"
    id "com.github.node-gradle.node" version "3.0.1"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'e2e', 'src/test/resources', '--tags', 'not @developing']
        }
    }
}

task cucumber_focus() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'e2e', 'src/test/resources', '--tags', '@focus']
        }
    }
}
task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack-cli/bin/cli.js')
}

dependencies {
    def cucumberVer = "5.7.0"
    def jacksonVer = "2.10.0"

    implementation(
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-thymeleaf",
            "org.springframework.boot:spring-boot-starter-data-mongodb",
            "org.apache.commons:commons-email:1.5",
            "commons-io:commons-io:2.8.0",
            "org.apache.commons:commons-lang3:3.12.0",
            "com.fasterxml.jackson.core:jackson-core:$jacksonVer",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVer",
            "com.fasterxml.jackson.core:jackson-annotations:$jacksonVer",
            "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jacksonVer"
    )
    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'com.icegreen:greenmail:1.6.2',
            'junit:junit:4.13.2',
            'org.mockito:mockito-core:3.7.7',
            'io.github.bonigarcia:webdrivermanager:4.3.1',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            "io.cucumber:cucumber-java:$cucumberVer",
            "io.cucumber:cucumber-junit:$cucumberVer",
            "io.cucumber:cucumber-spring:$cucumberVer"
    )
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

processResources.dependsOn webpack
test.dependsOn npm_test